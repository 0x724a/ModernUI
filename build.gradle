plugins {
    id 'maven-publish'
    id 'architectury-plugin' version '2.0.66'
    id 'forgified-fabric-loom' version '0.6.62' apply false
    id 'com.matthewprenger.cursegradle' version '1.4.0' apply false
    id 'com.github.johnrengelman.shadow' version '5.2.0' apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: 'architectury-plugin'
}

allprojects {
    apply plugin: 'java'
    dependencies {
        compileOnly "com.google.code.findbugs:jsr305:3.+"
    }

    version = "1.16.5-${mod_version}"
    group = 'icyllis.modernui'
    archivesBaseName = 'ModernUI'

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"

        // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
        // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
        // We'll use that if it's available, but otherwise we'll use the older option.
        def targetVersion = 8
        if (JavaVersion.current().isJava9Compatible()) {
            options.release.set(targetVersion)
        }
    }

    java {
        withSourcesJar()
    }
}