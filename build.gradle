import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "1.8.21" apply false
}

publishing {
    repositories {
        maven {
            name 'IzzelAliz'
            url 'https://maven.izzel.io/releases'
            credentials {
                username = project.findProperty("maven_user")
                password = project.findProperty("izzel_maven_token")
            }
        }
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group = 'icyllis.modernui'
    version = "${modernui_version}" as String

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    kotlin.jvmToolchain(17)

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
        compileOnly 'org.jetbrains:annotations:23.1.0'
        testCompileOnly 'com.google.code.findbugs:jsr305:3.0.2'
        testCompileOnly 'org.jetbrains:annotations:23.1.0'
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release.set(17)
    }

    tasks.withType(KotlinCompilationTask).configureEach {
        compilerOptions.jvmTarget.set(JvmTarget.JVM_17)
    }

    java {
        withSourcesJar()
    }
}
