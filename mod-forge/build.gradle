plugins {
    id 'forgified-fabric-loom'
    id 'signing'
    id 'com.matthewprenger.cursegradle'
    id 'com.github.johnrengelman.shadow'
}

configurations {
    shadow
}

architectury {
    platformSetupLoomIde()
}

repositories {
    maven {
        name "Progwml6 maven"
        url "https://dvs1.progwml6.com/files/maven/"
        content {
            includeGroup 'mezz.jei'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.architect.minecraft}"
    mappings minecraft.officialMojangMappings()
    forge "net.minecraftforge:forge:${rootProject.architect.minecraft}-${rootProject.forge_version}"

    modCompile "mezz.jei:jei-${rootProject.minecraft_version}:${rootProject.jei_version}"

    compile "com.github.ben-manes.caffeine:caffeine:${rootProject.caffeine_version}"
    compile "com.vladsch.flexmark:flexmark:${rootProject.flexmark_version}"

    shadow "com.github.ben-manes.caffeine:caffeine:${rootProject.caffeine_version}"
    shadow "com.vladsch.flexmark:flexmark:${rootProject.flexmark_version}"

    compile('org.apache.maven:maven-artifact') {
        version {
            strictly '3.6.3'
        }
    }

    compile(project(":")) {
        transitive = false
    }
    compile(project(":core")) {
        transitive = false
    }
    compileOnly(project(path: ":mod-common")) {
        transitive = false
    }
    runtimeOnly(project(path: ":mod-common", configuration: "transformForgeFakeMod")) {
        transitive = false
    }
    shadow(project(path: ":mod-common", configuration: "transformForge")) {
        transitive = false
    }
    shadow(project(":core")) {
        transitive = false
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadow]
    classifier "shadow"

    dependencies {
        exclude 'icu4j-59.1.jar'
        exclude 'commons-logging-1.2.jar'
    }

    relocate 'com.google.errorprone.annotations', 'icyllis.annotations'
    relocate 'org.checkerframe', 'icyllis.checkerframe'
    relocate 'com.github.benmanes.caffeine', 'icyllis.caffeine'
    relocate 'com.vladsch.flexmark', 'icyllis.flexmark'
    relocate 'org.intellij.lang.annotations', 'icyllis.annotations'
    relocate 'org.jetbrains.annotations', 'icyllis.annotations'

    exclude 'META-INF/CHANGES'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE**'
    exclude 'META-INF/NOTICE**'
    exclude 'META-INF/README**'
    exclude 'META-INF/maven/**'
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archiveFile)
    archiveClassifier = "universal"
}

processResources {

    inputs.property 'version', mod_version

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': mod_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
}

jar {
    classifier("slim")
    manifest {
        attributes([
                "Specification-Title"     : "ModernUI",
                "Specification-Vendor"    : "BloCamLimb",
                "Specification-Version"   : "2",
                "Implementation-Title"    : rootProject.name,
                "Implementation-Version"  : rootProject.version,
                "Implementation-Vendor"   : "BloCamLimb",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConnector"          : "icyllis.modernui.forge.MixinConnector"
        ])
    }
}

